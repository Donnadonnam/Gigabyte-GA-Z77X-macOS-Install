# Intel HD Graphics 4000 DSDT Patch
# Graphics/Intel-HD-Graphics-4000.txt
# Gigabyte GA-Z77X-UD5H DSDT Patch Repo - http://git.io/vIatr
#
# Enables graphics acceleration for Intel HD Graphics 4000
# Requires Integrated Graphics set to Enabled and VRAM Size set to 64MB in UEFI; designed for use with Intel HD Graphics 4000 only (no discrete graphics)
#
# Designed for use with Gigabyte Z77 motherboards; may work on other motherboard with edits
# Apply the GA-Z77X-UD5H Patch v2 before using this and look at the Graphics README.
#
# Special thanks to Piker-Alpha, RampageDev & toleda for their original edits; this would not have been possible if not for their work.

# 1. Enable graphics acceleration for Intel HD Graphics 4000

## Rename all instances of GFX0 to IGPU for Intel HD Graphics 4000
into device label GFX0 parent_label _SB.PCI0 set_label begin IGPU end;
into scope label _SB.PCI0.GFX0 set_label begin _SB.PCI0.IGPU end;
into method label GNOT parent_label IGPU code_regex (Notify\s\()GFX0(,\s[0-9A-Za-z]{4}\)) replaceall_matched begin %1IGPU%2 end;
into method label _L06 parent_label _GPE code_regex (\\_SB.PCI0.)GFX0(.GS[A-Z]{2}) replaceall_matched begin %1IGPU%2 end;
into method label OL1E parent_label _GPE code_regex (\\_SB.PCI0.)GFX0(\.([A-Z])LID) replaceall_matched begin %1IGPU%2 end;
into method label _STA parent_label H_EC code_regex (Store\s\(0x[0-9A-F]{2},\s\^\^\^)GFX0(.CLID\)) replace_matched begin %1IGPU%2 end;
into method label _WAK code_regex (\\_SB.PCI0.)GFX0(\.([A-Z])LID) replaceall_matched begin %1IGPU%2 end;
into method label BRTN code_regex (\\_SB.PCI0.)GFX0(\.DD0\d) replaceall_matched begin %1IGPU%2 end;

## Add device properties to IGPU device for Intel HD Graphics 4000 graphics acceleration
into method label _DSM parent_label IGPU remove_entry;
into device label IGPU insert begin
Method (_DSM, 4, NotSerialized)\n
{\n
	If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
	Return (Package () {\n
		"AAPL,ig-platform-id", Buffer() { 0x0A, 0x00, 0x66, 0x01 },\n
		"hda-gfx", Buffer() { "onboard-1" }\n
	})\n
}\n
end;

## Change the HDEF layout ID to 3 for Intel HD Graphics 4000 HDMI audio
into method label _DSM parent_label HDEF code_regex ("layout-id",\s*Buffer\s+\(0x04\)\s*\{\s*)0x[0-9A-F]{2},\s*0x[0-9A-F]{2},\s*0x[0-9A-F]{2},\s*0x[0-9A-F]{2} replace_matched begin %10x03,0x00,0x00,0x00 end;