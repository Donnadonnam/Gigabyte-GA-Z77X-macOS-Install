# AMD Radeon HD 7970 (Hamachi) DSDT Patch
# Graphics/AMD-Radeon-HD-7970-Hamachi.txt
# Gigabyte GA-Z77X-UD5H DSDT Patch Repo - http://git.io/vIatr
#
# Enables graphics acceleration for AMD Radeon HD 7970 and AirPlay Mirroring using Intel HD Graphics 4000
# Requires Init Display First set to PEG (PCIe Graphics) and Integrated Graphics set to Enabled in UEFI
#
# Designed for use with Gigabyte Z77 motherboards; may work on other motherboard with edits
# Apply the GA-Z77X-UD5H Patch v2 before using this and look at the Graphics README.
#
# Special thanks to Piker-Alpha, RampageDev & toleda for their original edits; this would not have been possible if not for their work.

# 1. Enable graphics acceleration for Intel HD Graphics 4000 AirPlay Mirroring

## Rename GFX0 device to IGPU for Intel HD Graphics 4000 AirPlay Mirroring
into device label GFX0 parent_label _SB.PCI0 set_label begin IGPU end;
into scope label _SB.PCI0.GFX0 set_label begin _SB.PCI0.IGPU end;
into method label GNOT parent_label IGPU code_regex (Notify\s\()GFX0(,\s[0-9A-Za-z]{4}\)) replaceall_matched begin %1IGPU%2 end;
into method label _L06 parent_label _GPE code_regex (\\_SB.PCI0.)GFX0(.GS[A-Z]{2}) replaceall_matched begin %1IGPU%2 end;
into method label OL1E parent_label _GPE code_regex (\\_SB.PCI0.)GFX0(\.([A-Z])LID) replaceall_matched begin %1IGPU%2 end;
into method label _STA parent_label H_EC code_regex (Store\s\(0x[0-9A-F]{2},\s\^\^\^)GFX0(.CLID\)) replace_matched begin %1IGPU%2 end;
into method label _WAK code_regex (\\_SB.PCI0.)GFX0(\.([A-Z])LID) replaceall_matched begin %1IGPU%2 end;
into method label BRTN code_regex (\\_SB.PCI0.)GFX0(\.DD0\d) replaceall_matched begin %1IGPU%2 end;

## Add device properties to IGPU device for Intel HD Graphics 4000 AirPlay Mirroring graphics acceleration
# Note: Hides the IGPU and disables its outputs (same as iMac); Quick Sync remains available for AirPlay Mirroring (use iMac13,X SMBIOS)
into method label _DSM parent_label IGPU remove_entry;
into device label IGPU insert begin
Method (_DSM, 4, NotSerialized)\n
{\n
	If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
	Return (Package () {\n
		"AAPL,ig-platform-id", Buffer() { 0x07, 0x00, 0x62, 0x01 }\n
	})\n
}\n
end;

# 2. Enable graphics acceleration for AMD Radeon HD 7970

## Rename PEGP device to GFX0 for AMD Radeon HD 7970
into scope label _SB.PCI0.PEG0.PEGP set_label begin _SB.PCI0.PEG0.GFX0 end;
into_all all code_regex _SB.PCI0.PEG0.PEGP replaceall_matched begin _SB.PCI0.PEG0.GFX0 end;

## Add device properties to GFX0 device for AMD Radeon HD 7970 graphics acceleration
into device label PEGP parent_label PEG0 remove_entry;
into device label PEG0 insert begin
Device (GFX0)\n
{\n
	Name (_ADR, Zero)\n
	Method (_DSM, 4, NotSerialized)\n
	{\n
		If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
		Return (Package () {\n
			"AAPL,slot-name", Buffer() { "Slot-1" },\n
			"@0,name", Buffer() { "ATY,Hamachi" },\n
			"@1,name", Buffer() { "ATY,Hamachi" },\n
			"@2,name", Buffer() { "ATY,Hamachi" },\n
			"@3,name", Buffer() { "ATY,Hamachi" },\n
			"device-id", Buffer() { 0x98, 0x67, 0x00, 0x00 },\n
			"device_type", Buffer() { "Display Controller" },\n
			"model", Buffer() { "AMD Radeon HD 7970" },\n
			"hda-gfx", Buffer() { "onboard-1" }\n
		})\n
	}\n
}\n
end;

## Add the HDAU device with device properties for AMD Radeon HD 7970 HDMI audio
into device label PEG0 insert begin
Device (HDAU)\n
{\n
	Name (_ADR, One)\n
	Method (_DSM, 4, NotSerialized)\n
	{\n
		If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
		Return (Package () {\n
			"hda-gfx", Buffer() { "onboard-1" }\n
		})\n
	}\n
}\n
end;